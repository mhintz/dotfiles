" plugins with vim-plug
call plug#begin('~/.vim/bundle')
" tmux integration
Plug 'vim-airline/vim-airline'
Plug 'christoomey/vim-tmux-navigator'
Plug 'tmux-plugins/vim-tmux-focus-events'
" highlighting
Plug 'sheerun/vim-polyglot'
Plug 'alunny/pegjs-vim'
Plug 'junegunn/seoul256.vim'
Plug 'rakr/vim-one'
" ergonomics
Plug 'chaoren/vim-wordmotion'
Plug 'tpope/vim-rsi'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-surround'
Plug 'Raimondi/delimitMate'
Plug 'editorconfig/editorconfig-vim'
" utilities
Plug 'junegunn/fzf.vim'
Plug 'wincent/ferret'
Plug 'scrooloose/nerdtree'
Plug 'neoclide/coc.nvim', {'do': 'yarn install --frozen-lockfile'}
Plug 'neoclide/coc-prettier', {'do': 'yarn install --frozen-lockfile'}
Plug 'neoclide/coc-eslint', {'do': 'yarn install --frozen-lockfile'}
Plug 'neoclide/coc-tsserver', {'do': 'yarn install --frozen-lockfile'}
Plug 'neoclide/coc-rls', {'do': 'yarn install --frozen-lockfile'}
Plug 'neoclide/coc-python', {'do': 'yarn install --frozen-lockfile'}
Plug 'neoclide/coc-json', {'do': 'yarn install --frozen-lockfile'}
" maybe
" Plug 'edkolev/tmuxline.vim'
" Plug 'ervandew/supertab'
" Plug 'craigemery/vim-autotag'
" Plug 'jason0x43/vim-js-indent'
" Plug 'tpope/vim-unimpaired'
call plug#end()

" compatibility mode
set nocompatible

" encoding
set encoding=utf-8

" turn on filetype, plugin, and indent
filetype plugin indent on

" ***** THEME AND HIGHLIGHTING ******

" syntax highlighting
syntax on

" optimizations for rendering speed
" (optional) force the old regex engine, which is a bit faster
if !has('nvim')
  set re=1
  " actually not needed for neovim but helps for vim
  set ttyfast
endif
" lazily redraw the screen
set lazyredraw

" these print formatting chars are required for truecolor support in vim
" set t_Co=256
" set t_8b=[48;2;%lu;%lu;%lum
" set t_8f=[38;2;%lu;%lu;%lum
" get those nice colors in nvim's terminal
if has('nvim')
  set termguicolors
endif

" seoul256 colors for built-in terminal, also fzf preview window
let g:terminal_color_0  = '#606060'
let g:terminal_color_1  = '#df9a98'
let g:terminal_color_2  = '#719672'
let g:terminal_color_3  = '#e0bb71'
let g:terminal_color_4  = '#96bbdc'
let g:terminal_color_5  = '#dfbdbc'
let g:terminal_color_6  = '#97bcbc'
let g:terminal_color_7  = '#d8d8d8'
let g:terminal_color_8  = '#757575'
let g:terminal_color_9  = '#e07798'
let g:terminal_color_10 = '#97bb98'
let g:terminal_color_11 = '#ffdd98'
let g:terminal_color_12 = '#badcfb'
let g:terminal_color_13 = '#ffbebc'
let g:terminal_color_14 = '#96ddde'
let g:terminal_color_15 = '#e9e9e9'

" seoul256 background to match terminal
let g:seoul256_background = 237
set background=dark
colorscheme seoul256

" other themes

" vim-one
" let g:one_allow_italics = 1
" set background=dark
" colorscheme one

" colorscheme OceanicNext
" colorscheme neuromancer
" colorscheme vice

" set vim-airline theme to match
let g:airline_theme='one'
" let g:airline_theme='zenburn'

" ****** SETTINGS ******
" set synmaxcol=256
" syntax sync minlines=128

" mouse controls if desired
set mouse=a

" reloads when a file is changed in the background
set autoread

" incremental search
set incsearch
" toggle highlighted search on/off
" set hlsearch
set nohlsearch

" expected backspace behavior (backspace can delete over indent, eol, and the
" start of your editing)
set backspace=indent,eol,start
" and arrow key movement wraps around line breaks
" set whichwrap+=<,>

" history settings
set history=50

" vim "UI" settings
set ruler
set number
set relativenumber
set cmdheight=2

" settings which help CoC.nvim servers
set nobackup
set nowritebackup
set updatetime=1000

" set vim directories for undo, backup, and swap files
" set backup
" set backupdir=$HOME/.vim/.backup//
set undodir=$HOME/.vim/.undo//
set directory^=$HOME/.vim/.swp//

" try to keep the cursor at least 3 lines from the edge of the window
set scrolloff=3

" allow files to be open as buffers without being visible
set hidden

" line wrap settings
set wrap
set breakindent
set breakindentopt=sbr
" Unicode curly arrow, space
set showbreak=â†ª

" default indentation settings
set autoindent
set smartindent
set tabstop=2
set shiftwidth=2
set expandtab
set softtabstop=2

" completion menu options
set completeopt=menuone
set shortmess+=c

" netrw settings
" augroup NETRW
"   autocmd! FileType netrw
"   autocmd FileType netrw setl bufhidden=delete
" augroup END

" ****** SET LEADER KEY ******
let mapleader = " "

" ****** PLUGIN SETTINGS ******

" autotags options
" let g:autotagTagsFile='.tags'
" let g:autotagCtagsCmd = 'rg --files | ctags -R'

" delimitMate Settings
let g:delimitMate_expand_cr = 1

" NERDTree Settings
nnoremap <leader>o :NERDTreeToggle<CR>
let g:NERDTreeWinSize=30
let g:NERDTreeShowHidden=1
" open a NERDTree automatically (and move to the main window) when vim is opened or a new tab is made
" autocmd vimenter * NERDTree | wincmd l
" autocmd tabnew * NERDTree | wincmd l
" close the nerd tree window if it's the last one open
" autocmd bufenter * if (winnr("$") == 1 && exists("b:NERDTree") && b:NERDTree.isTabTree()) | q | endif

" vim-airline disable tmuxline integration (it doesn't render correctly)
let g:airline#extensions#tmuxline#enabled = 0
let g:airline#extensions#tabline#enabled = 1

" Tmuxline (styles get overriden incorrectly, need to refresh it on enter
" let g:tmuxline_powerline_separators = 0

" Supertab
" let g:SuperTabDefaultCompletionType = "context"

" FZF + ripgrep

" add to runtime path
set rtp+=/usr/local/opt/fzf
set rtp+=/usr/local/bin/rg

command! -bang -nargs=* Rg
  \ call fzf#vim#grep(
  \   'rg --hidden --column --line-number --no-heading --color=always '.shellescape(<q-args>), 1,
  \   <bang>0 ? fzf#vim#with_preview('up:60%')
  \           : fzf#vim#with_preview('right:50%:hidden', '?'),
  \   <bang>0)

command! -bang -nargs=? -complete=dir Files
  \ call fzf#vim#files(<q-args>,
  \   <bang>0 ? fzf#vim#with_preview('up:50%')
  \           : fzf#vim#with_preview('right:40%'),
  \   <bang>0)

" for styling the FZF window
augroup FZF
  autocmd! FileType fzf
  autocmd  FileType fzf set laststatus=0 noshowmode noruler
    \| autocmd BufLeave <buffer> set laststatus=2 showmode ruler
augroup END

" some fun fzf completions
imap <c-x><c-l> <plug>(fzf-complete-line)
imap <c-x><c-f> <plug>(fzf-complete-path)

" Ferret
" turn off automatic key mappings
let g:FerretMap = 0
" insist on rg
let g:FerretExecutable='rg'
" args to pass to rg
let g:FerretExecutableArguments = {
  \ 'rg': '--hidden --vimgrep --no-heading --no-config --max-columns 4096'
  \ }

" CoC-NVIM

" tab completion
inoremap <silent><expr> <TAB>
      \ pumvisible() ? "\<C-n>" :
      \ <SID>check_back_space() ? "\<TAB>" :
      \ coc#refresh()
inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<C-h>"
" use enter to confirm completions - but breaks expansion of brackets on enter
" inoremap <expr> <CR> pumvisible() ? "\<C-y>" : "\<C-g>u\<CR>"

function! s:check_back_space() abort
  let col = col('.') - 1
  return !col || getline('.')[col - 1]  =~# '\s'
endfunction

function! s:show_documentation()
  if (index(['vim','help'], &filetype) >= 0)
    execute 'vert bo help '.expand('<cword>')
  else
    call CocAction('doHover')
  endif
endfunction

" format function (probably needs work / configuration)
command! -nargs=0 Format :call CocAction('format')

" keymaps for CoC
nmap <leader>rsc :CocRestart<CR>

" conditional keybindings scoped to certain file types
let s:CoC_Whitelist = ["typescript", "typescript.tsx", "typescriptreact", "python", "rust", "go", "vim", "help"]
let s:CoC_Prettier_Types = ["typescript", "typescript.tsx", "typescriptreact", "javascript", "javascript.jsx", "javascriptreact"]
function! CoC_key_maps()
  if (index(s:CoC_Whitelist, &filetype) >= 0)
    " keymaps for diagnostic info
    nmap <buffer> <silent> <leader>D <Plug>(coc-diagnostic-info)
    nmap <buffer> <silent> [c <Plug>(coc-diagnostic-prev)
    nmap <buffer> <silent> ]c <Plug>(coc-diagnostic-next)
    " keymaps for calling the show_documentation function defined above
    nnoremap <buffer> <silent> <leader>K :call <SID>show_documentation()<CR>
    nnoremap <buffer> <silent> gh :call <SID>show_documentation()<CR>
    " keymaps for jumping around the code
    nmap <buffer> <silent> <C-]> <Plug>(coc-definition)
    nmap <buffer> <silent> gt <Plug>(coc-type-definition)
    nmap <buffer> <silent> gi <Plug>(coc-implementation)
    nmap <buffer> <silent> gr <Plug>(coc-references)
    nmap <buffer> <leader>R <Plug>(coc-rename)
    nmap <buffer> <leader>rf <Plug>(coc-refactor)
    " these commands don't work as expected on the filetypes where CoC is set up (right now)
    " nmap <buffer> <silent> gd <Plug>(coc-declaration)
    " nmap <buffer> <silent> gf <Plug>(coc-openlink)
  endif
  " prettier keybind
  if (index(s:CoC_Prettier_Types, &filetype) >= 0)
    nmap <buffer> <leader>F :CocCommand prettier.formatFile<CR>
  endif
  if (index(s:CoC_Prettier_Types, &filetype) >= 0)
    nmap <buffer> <leader>L :CocCommand eslint.executeAutofix<CR>
  endif
endfunction

function! CoC_notify_init()
  echohl MoreMsg | echom 'coc.nvim initialized!' | echohl None
endfunction

augroup CoC
  autocmd!
  autocmd FileType * call CoC_key_maps()
  autocmd User CocNvimInit call CoC_notify_init()
augroup END

" Random
function! g:PrintFullFilePath()
    echo expand('%:p')
endfunction

" ****** Special Case Syntax ******
augroup HmmLangSyntax
  autocmd!
  autocmd BufNewFile,BufRead *.hmm set syntax=scheme
augroup END

" ****** KEYBINDINGS ******

" remap so getting to the command line is one key away
nnoremap ; :

" manually reload vim config
nnoremap <leader>rv :source ~/.vim/vimrc<CR>
" open help in a side window
nnoremap <leader>H :vert bo help<CR>

" ****** BUFFERS, WINDOWS, AND TABS ******
nnoremap <leader>wq :wq<CR>
nnoremap s :w<CR>
nnoremap <leader>qq :bp\|bd #<CR>
" (Ctrl-w q quits completely)
nnoremap <leader>qw :q<CR>
nnoremap <leader>qa :qa<CR>
nnoremap <leader>qc :%bdelete<CR>

" fzf-vim mappings
nnoremap <C-p> :Files!<CR>
nnoremap <leader>a :Rg!<Space>
nnoremap <leader>A yiW:Rg! <C-R>"
nnoremap <leader>p :Buffers<CR>
nnoremap <leader>f :BLines<CR>
nnoremap <leader>n yiW:BLines <C-R>"
nnoremap <leader>h :Helptags<CR>
nnoremap <leader>P :History<CR>
nnoremap <leader>C :Commands<CR>

" ferret mappings
nnoremap <leader>S :Ack<Space>
nnoremap <leader>SR :Acks<Space>

" quickfix and location list window toggles
nmap <leader>co :copen<CR>
nmap <leader>cc :cclose<CR>
nmap <leader>lo :lopen<CR>
nmap <leader>lc :lclose<CR>

" edit the last-edited file
nnoremap <leader><leader> :e#<CR>

" handling buffers
nnoremap <leader>bn :enew<CR>
nnoremap <leader>bd :bdelete<CR>
nnoremap <leader>bq :bdelete!<CR>
nnoremap <leader>bb <C-^>

" move between buffers / tabs
noremap <leader><Left> :bprev<CR>
noremap <leader><Right> :bnext<CR>

" map <leader># to switch to buffer # (among listed buffers)
function! g:OpenBufByIdx(index)
    let l:bufs = map(filter(copy(getbufinfo()), 'v:val.listed'), 'v:val.bufnr')
    if a:index < len(bufs)
        execute ":b ".get(bufs, a:index)
    endif
endfunction

" <leader># opens buffer number # from the buffer list
for i in range (1, 9)
    execute 'nnoremap <leader>'.i.' :call OpenBufByIdx('.(i - 1).')<CR>'
endfor

" ****** EDITING COMMANDS ******
" turn off Ctrl-Z since it's annoying
noremap <C-z> <Nop>

" macro stuff
noremap q <Nop>
nnoremap Q q

" clear search highlights if they add visual clutter
noremap <leader>dh :nohlsearch<CR>

" start typing a subsitution command for an identifier within a file
nnoremap <leader>s yiW:%s/<C-R>"/

" remove trailing whitespace
nnoremap <leader>ds :%s/\s\+$//e<CR>

" Opposite of join (breaK?).
" A space is replaced with a carriage return; otherwise a carriage return is inserted.
nnoremap <expr> K getline('.')[col('.') - 1] == ' ' ? "r<CR>" : "i<CR><ESC>l"

" duplicate line
nnoremap <leader>ld yyp

" move line downward by inserting a line above it
nnoremap <leader>ll mzO<ESC>`z

" cursor and scrolling
noremap <M-n> 6<C-e>6j
noremap <M-m> 6<C-y>6k

noremap <M-j> 6j
noremap <M-k> 6k

noremap <C-M-n> 6<C-e>
noremap <C-M-m> 6<C-y>

" half-pages
noremap <M-d> <C-d>
noremap <M-u> <C-u>

" pages
noremap <M-f> <C-f>
noremap <M-b> <C-b>

" start / end of line movement like emacs
noremap <C-a> ^
noremap <C-e> $

" these bindings overwrite the ones provided by vim-rsi
inoremap <C-a> <C-o>^
inoremap <C-e> <C-o>$

cnoremap <C-a> <Home>
cnoremap <C-e> <End>

" use this to paste from system pasteboard
nnoremap <leader>v :set paste<CR>"*gp:set nopaste<CR>

" delete to end of line
inoremap <C-k> <C-o>d$

" move lines of text around vertically
nnoremap <C-M-j> mz:m+<cr>`z
nnoremap <C-M-k> mz:m-2<cr>`z
vnoremap <C-M-j> :m'>+<cr>`<my`>mzgv`yo`z
vnoremap <C-M-k> :m'<-2<cr>`>my`<mzgv`yo`z
